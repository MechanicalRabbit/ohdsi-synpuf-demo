# Acute myocardial infarction events

This test case demonstrates the construction of a domain specific
language specific to the generation of OHDSI Cohorts. To get started,
we'll import some boilerplate definitions. This will create the
*DataKnot* called `sp10` that represents our test database.

    include("copybook.jl")

We're going to follow the generated SQL found in `1770674.sql` that was
produced by the JSON found in `1770674.json`. This SQL isn't hand
crafted, so our work here shouldn't be compared directly to SQL.
Before we start our translation, let's run our first query.

    @query sp10 count(person)
    #=>
    ┼────┼
    │ 10 │
    =#

## Concept Coding

We could list all drug exposure concepts.

    @query sp10 begin
        drug_exposure
        group(concept)
        { concept.concept_code, count(drug_exposure) }
    end
    #=>
       │ concept_code  #B │
    ───┼──────────────────┼
     1 │ 311671         1 │
     2 │ 198188         1 │
     3 │ 153666         1 │
     4 │ 197499         1 │
     5 │ 197770         2 │
     6 │ 308964         1 │
     7 │ 310798         1 │
     8 │ 314076         1 │
     9 │ 314077         1 │
    10 │ 858810         2 │
    11 │ 905395         1 │
    =#

We could list all concepts that are considered hypertensive.

    IsHypertensive = IsCoded("SNOMED", 38341003)

    @query sp10 concept.filter($IsHypertensive){concept_name}
    #=>
      │ concept                          │
      │ concept_name                     │
    ──┼──────────────────────────────────┼
    1 │ Hypertensive disorder            │
    2 │ Renovascular hypertension        │
    3 │ Malignant essential hypertension │
    4 │ Malignant secondary hypertension │
    5 │ Secondary hypertension           │
    6 │ Hypertensive crisis              │
    =#

We could define a code set and use it to check for the occurrence of a
particular condition.

    @query sp10 begin
        condition
        filter(concept.$IsHypertensive)
        {person_id, concept.concept_name}
    end
    #=>
       │ condition                                   │
       │ person_id  concept_name                     │
    ───┼─────────────────────────────────────────────┼
     1 │      1780  Renovascular hypertension        │
     2 │      1780  Secondary hypertension           │
     ⋮
    15 │    110862  Malignant essential hypertension │
    =#

Let's consider specified visit types.

    IsInpatientOrER = IsCoded("Visit", "ERIP", "ER", "IP")

    @query sp10 begin
        visit
        filter(concept.$IsInpatientOrER)
        {person_id, concept.concept_name}
    end
    #=>
      │ visit                      │
      │ person_id  concept_name    │
    ──┼────────────────────────────┼
    1 │      1780  Inpatient Visit │
    2 │     30091  Inpatient Visit │
    3 │     69985  Inpatient Visit │
     ⋮
    =#

## Observation Period

For tables in OHDSI CDM, `begin_date` is mandatory, but for many
entities, `end_date` is optional. The OHDSI cohort queries typically
treat a missing ending as the start time plus one day, we call that
`end_date_or_next_day`. It's not clear why these queries pick one day
offset like this, or a simple coalesce could be used.

    @query sp10 condition{it, person, start_date, end_date_or_next_day}
    #=>
       │ condition                                           │
       │ condition  person  start_date  end_date_or_next_day │
    ───┼─────────────────────────────────────────────────────┼
     1 │ 228161     1780    2008-04-10  2008-04-10           │
     2 │ 228213     1780    2009-05-22  2009-05-22           │
     3 │ 228060     1780    2008-11-22  2008-11-23           │
     4 │ 3767867    30091   2008-11-12  2008-11-12           │
     ⋮
    26 │ 13769162   110862  2009-09-30  2009-10-01           │
    =#

We can use `its_observation_period` to return the correlated
observation period. This could let us compute the primary events
for this particular measure.

    IsMyocardialInfarction =
        IsCoded("SNOMED", 22298006, 1755008)

    @query sp10 begin
        condition
        keep(op => its_observation_period(prior=0days, after=0days))
        filter(concept.$IsMyocardialInfarction)
        { person, it.start_date, end_date => it.end_date_or_next_day,
          op_start => op.start_date, op_end => op.end_date}
    end
    #=>
       │ condition                                              │
       │ person  start_date  end_date    op_start    op_end     │
    ───┼────────────────────────────────────────────────────────┼
     1 │ 1780    2008-04-10  2008-04-10  2008-02-23  2009-08-01 │
     2 │ 30091   2009-08-02  2009-08-03  2008-02-09  2010-07-20 │
     ⋮
    11 │ 110862  2009-09-30  2009-10-01  2008-01-04  2010-09-13 │
    =#

These primary events can be further narrowed down by finding
overlapping visits which meet additional criteria.

    @query sp10 begin
        condition
        keep(op=>its_observation_period(prior=0days, after=0days))
        filter(concept.$IsMyocardialInfarction)
        filter(exists(overlapping_visit(prior=0days, after=0days).
                      filter(concept.$IsInpatientOrER)))
        { person, it.start_date, end_date => it.end_date_or_next_day,
          op_start => op.start_date, op_end => op.end_date}
    end
    #=>
      │ condition                                              │
      │ person  start_date  end_date    op_start    op_end     │
    ──┼────────────────────────────────────────────────────────┼
    1 │ 1780    2008-04-10  2008-04-10  2008-02-23  2009-08-01 │
    2 │ 30091   2009-08-02  2009-08-03  2008-02-09  2010-07-20 │
    3 │ 69985   2010-07-22  2010-07-30  2008-02-07  2010-11-14 │
    4 │ 95538   2009-03-30  2009-04-03  2008-02-22  2010-05-19 │
    5 │ 107680  2009-07-20  2009-07-30  2008-02-09  2010-12-30 │
    6 │ 110862  2009-09-30  2009-10-01  2008-01-04  2010-09-13 │
    =#
