# Acute myocardial infarction events

This test case demonstrates the construction of a domain specific
language specific to the generation of OHDSI Cohorts. To get started,
we'll import some boilerplate definitions. This will create the
*DataKnot* called `sp10` that represents our test database.

    Base.include(@__MODULE__, "$(@__DIR__)copybook.jl")

We're going to follow the generated SQL found in `1770674.sql` that was
produced by the JSON found in `1770674.json`. This SQL isn't hand
crafted, so our work here shouldn't be compared directly to SQL.
Before we start our translation, let's run our first query.

    @query sp10 count(person)
    #=>
    ┼────┼
    │ 10 │
    =#

OHDSI has it's own concept identifiers, while one could work with
those, it may be more helpful in a query to use concept code as
provided from a given vocabulary. Let's define `SNOMED` to translate
from the `SNOMED` vocabulary to OHDSI's internal concept identifiers.

    SNOMED(id) = @query begin
                     concept
                     filter((vocabulary_id=="SNOMED") &
                            (concept_code==string($id)))
                     concept_id
                     is1to1()
                 end

Then we could use SNOMED codes and have them automatically translated.

    @query sp10 $SNOMED(22298006)
    #=>
    │ concept_id │
    ┼────────────┼
    │    4329847 │
    =#

We could list all drug exposure concepts.

    @query sp10 begin
        drug_exposure
        group(concept)
        { concept.concept_code, count(drug_exposure) }
    end
    #=>
       │ concept_code  #B │
    ───┼──────────────────┼
     1 │ 311671         1 │
     2 │ 198188         1 │
     3 │ 153666         1 │
     4 │ 197499         1 │
     5 │ 197770         2 │
     6 │ 308964         1 │
     7 │ 310798         1 │
     8 │ 314076         1 │
     9 │ 314077         1 │
    10 │ 858810         2 │
    11 │ 905395         1 │
    =#

We could list all concepts that are considered hypertensive.

    is_hypertensive = @query iscoded("SNOMED","38341003")
    @query sp10 concept.filter($is_hypertensive){concept_name}
    #=>
      │ concept                          │
      │ concept_name                     │
    ──┼──────────────────────────────────┼
    1 │ Hypertensive disorder            │
    2 │ Renovascular hypertension        │
    3 │ Malignant essential hypertension │
    4 │ Malignant secondary hypertension │
    5 │ Secondary hypertension           │
    6 │ Hypertensive crisis              │
    =#

We could define a code set and use it to check for the occurrence of a
particular condition. Let's list only 3 records.

    IsHypertensive = IsCoded("SNOMED", 38341003)

    @query sp10 begin
        condition
        filter(concept.$IsHypertensive)
        {person_id, concept.concept_name}
        take(3)
    end
    #=>
      │ condition                                   │
      │ person_id  concept_name                     │
    ──┼─────────────────────────────────────────────┼
    1 │      1780  Renovascular hypertension        │
    2 │      1780  Secondary hypertension           │
    3 │     30091  Malignant secondary hypertension │
    =#

Let's look for MI conditions.

    IsMyocardialInfarction =
        IsCoded("SNOMED", 22298006, 1755008)

    @query sp10 begin
        condition
        filter(concept.$IsMyocardialInfarction)
        {person_id, concept.concept_name}
        take(3)
    end
    #=>
      │ condition                                                    │
      │ person_id  concept_name                                      │
    ──┼──────────────────────────────────────────────────────────────┼
    1 │      1780  Acute myocardial infarction                       │
    2 │     30091  Acute subendocardial infarction                   │
    3 │     37455  Acute myocardial infarction of anterolateral wall │
    =#

Let's consider specified visit types.

    IsInpatientOrER = IsCoded("Visit", "ERIP", "ER", "IP")

    @query sp10 begin
        visit
        filter(concept.$IsInpatientOrER)
        {person_id, concept.concept_name}
        take(3)
    end
    #=>
      │ visit                      │
      │ person_id  concept_name    │
    ──┼────────────────────────────┼
    1 │      1780  Inpatient Visit │
    2 │     30091  Inpatient Visit │
    3 │     69985  Inpatient Visit │
    =#

The `begin_date` and `end_date` are normalized across
tables, and `end_date` defaults to begining date if it
is not provided in the underlying data.

    @query sp10 condition{it, start_date, end_date}
    #=>
       │ condition                         │
       │ condition  start_date  end_date   │
    ───┼───────────────────────────────────┼
     1 │ 228161     2008-04-10  2008-04-10 │
     2 │ 228213     2009-05-22  2009-05-22 │
     3 │ 228060     2008-11-22  2008-11-23 │
     4 │ 3767867    2008-11-12  2008-11-12 │
     ⋮
    26 │ 13769162   2009-09-30  2009-10-01 │
    =#
